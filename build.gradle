buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'org.hidetake:gradle-ssh-plugin:2.9.0'
  }
}

apply plugin: 'org.hidetake.ssh'
apply plugin: 'idea'

group 'konra'
version '0.2'

task buildApp(type: Exec) {
  group 'app'
  workingDir = projectDir
  commandLine = ['cmd', '/c', 'ng', 'build', '--prod']
  inputs.files fileTree('src')
  outputs.dir 'dist'
}

task pack(type: Zip) {
  group 'app'
  from 'dist/'
  include '*', '**/*'
  archiveName project.property('bundle.name').toString()
  destinationDir file("${projectDir}/bundle")
  inputs.files fileTree('dist')
  outputs.dir file("${projectDir}/bundle")

  dependsOn buildApp
}

task clean(type: Delete) {
  group 'app'
  delete file('bundle'), file('dist')
}

remotes {
  droplet {
    host = '138.197.178.123'
    user = 'root'
    identity = file(project.property('key.path'))
  }
}

ssh.settings {
  knownHosts = allowAnyHosts
}

def deployPath = project.property('deploy.path')

task deploy {
  group 'app'

  doLast {
    ssh.run {

      session(remotes.droplet) {
        execute "sudo rm -rf ${deployPath}/*"
        put from: "bundle/${project.property('bundle.name')}", into: "${deployPath}/"
        execute "unzip ${deployPath}/${project.property('bundle.name')} -d ${deployPath}"
      }
    }
  }

  dependsOn pack
}


